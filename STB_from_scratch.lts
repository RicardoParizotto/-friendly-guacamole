const Tempo = 4
const LimiteAlarme = 3
const TamanhoRessalva = 5

fluent AlertaRessalva = <r.chegada_ressalva[LimiteAlarme], r.retirada_ressalva[LimiteAlarme]> initially 0

// Bagagens do checkin nunca vão para esteira de recolhimento
assert P1 = [](c.checkin_bagagem -> !er.chegada_recolhimento)

// Bagagens que não passarem na inspeção jamais são embarcadas
assert P2 = [](i.inspecao_erro -> !s.encaminha_voo)

// Bagagens devem passar na inspeção antes de serem embarcadas
property Embarque = ({c.checkin_bagagem, d.conexao_bagagem} -> i.inspecao_ok -> s.encaminha_voo -> Embarque).

// =====================================================================================================
// === Componentes STB =================================================================================

// Checkin
CHECKIN = (checkin_bagagem -> coloca_identificador -> encaminha_inspecao -> CHECKIN).

// Desembarque de bagagem 
DESEMBARQUE = (chegada_voo -> (destino_bagagem -> DESTINO | conexao_bagagem -> CONEXAO)),
DESTINO = (encaminha_recolhimento -> DESEMBARQUE),
CONEXAO = (encaminha_inspecao -> DESEMBARQUE).

// Inspecionamento
INSPECAO = (chegada_inspecao -> (realiza_inspecao -> INSPECAO_OK | realiza_inspecao -> INSPECAO_ERRO)),
INSPECAO_OK = (inspecao_ok -> encaminha_selecao -> INSPECAO),
INSPECAO_ERRO = (inspecao_erro -> encaminha_ressalva -> INSPECAO).

// Embarque de bagagem
SELECAO = (chegada_selecao -> le_identificador -> encaminha_voo -> SELECAO).

// Recolhimento da bagagem após desembarque
RECOLHIMENTO = (chegada_recolhimento -> RECOLHIMENTO[0]),
RECOLHIMENTO[i:0..Tempo] = (when (i < Tempo) espera -> RECOLHIMENTO[i+1] 
						  | when (i <= Tempo) recolhe_bagagem -> RECOLHIMENTO
						  | when (i == Tempo) encaminha_ressalva -> RECOLHIMENTO).

// Bagagens que falharam inspeção ou foram abandonadas
RESSALVA = RESSALVA[0],
RESSALVA[n:0..TamanhoRessalva] = (
	  when (n < TamanhoRessalva) chegada_ressalva[n] -> RESSALVA[n+1]
	| when (n > 0) retirada_ressalva[n] -> RESSALVA[n-1]).

// Módulos do Sistema de Tratamento de Bagagens
|| STB = ( c:CHECKIN || d:DESEMBARQUE || i:INSPECAO || s:SELECAO || er:RECOLHIMENTO || r:RESSALVA )
/{
	{c.encaminha_inspecao, d.encaminha_inspecao}  / i.chegada_inspecao,

	d.encaminha_recolhimento / er.chegada_recolhimento,
	
	i.encaminha_selecao / s.chegada_selecao,
	{i.encaminha_ressalva, er.encaminha_ressalva} / r.chegada_ressalva
}.
